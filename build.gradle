ext {
    aGroup = 'jullill'
    aVersion = '0.0.1-SNAPSHOT'
}

//buildscript {
//    repositories {
//        mavenLocal()
//        maven {
//            url uri('../repo')
//        }
//    }
//
//    dependencies {
//        classpath group: 'org.github.julil', name: 'gradle-plugins', version: '1.0-SNAPSHOT'
//    }
//}
apply plugin: 'groovy'
apply plugin: IdeaToGradleConverterPlugin

IdeaToGradleConverter.libraryAliases = [
    'gwt-dev' : [group: 'com.google.gwt', name: 'gwt-dev', version: '2.7.0']
]

dependencies {
    compile gradleApi()
    compile localGroovy()
}

class IdeaToGradleConverterPlugin implements Plugin<Project> {

    static def parseIdeaLibraries(ArrayList<File> libraries, Project project) {
        def result = [:]
        def projectPath = project.projectDir.absolutePath
        libraries.each {
            def rootTag = new XmlParser().parse(it)

            result += [(rootTag.library.@name.get(0)):
                               [
                                       'classesPath': replaceProjectPath(rootTag.library.CLASSES.root.@url, projectPath),
                                       'javaDocPath': replaceProjectPath(rootTag.library.JAVADOC.root.@url, projectPath),
                                       'sourcesPath': replaceProjectPath(rootTag.library.SOURCES.root.@url, projectPath)
                               ]
            ]
        }
        result
    }

    /**
     * Получает пути к iml-файлам модулей, подключенных в IDEA
     * @return String[] - пути к модулям
     */
    static def parseIdeaProjectModules(File modulesFile, Project project) {
        def projectPath = project.projectDir.absolutePath
        def root = new XmlParser().parse(modulesFile)
        root.find { it.@name == 'ProjectModuleManager' }.collect {
            replaceProjectPath(it.module.@filepath, projectPath)
        }.first().collect { moduleImlPath ->
            File moduleImlFile = project.file(moduleImlPath)
            moduleImlFile
        }
    }

    static def replaceProjectPath(ArrayList<Path> path, String projectPath) {
        projectPath = projectPath.replace('\\', '/')
        path.collect {
            it.replaceAll('jar:\\/\\/', '').replaceAll('!\\/', '').replace('\$PROJECT_DIR\$', projectPath)
        }
    }

    static def cleanSourcePath(String url) {
        url.replaceAll('file:\\/\\/\\$MODULE_DIR\\$\\/', '')
    }

    static def generateBuildScript(File file, Map libraries, Project project) {
        File buildFile = new File(file.parent + "/build.gradle")
        def root = new XmlParser().parse(file)

        def sourceSetsOutput = generateSourceSets(root.component.content.sourceFolder)
        if (!sourceSetsOutput.empty) {
            buildFile << sourceSetsOutput + "\n\n"
        }
        def dependencies = generateDependencies(root.component.orderEntry, libraries, project)
        if (!dependencies.empty) {
            buildFile << dependencies + "\n\n"
        }
    }

    def static generateSourceSets(ArrayList<Path> sourceList) {
        def sources = [
                'main': [
                        'java'     : [],
                        'resources': []
                ],
                'test': [
                        'java'     : [],
                        'resources': []
                ]
        ]
        sourceList.each {
            def path = cleanSourcePath(it.@url)
            if (it.@isTestSource == 'false') {
                sources['main']['java'] += "'${path}'"
            } else if (it.@isTestSource == 'true') {
                sources['test']['java'] += "'${path}'"
            } else if (it.@type == 'java-resource') {
                sources['main']['resources'] += "'${path}'"
            } else if (it.@type == 'java-test-resource') {
                sources['test']['resources'] += "'${path}'"
            }
        }

        def javaSourcesOutput = ""
        if (!sources['main']['java'].empty) {
            javaSourcesOutput = """java {
                                    |           srcDirs = ${sources['main']['java']}
                                    |       }""".stripMargin()
        }
        def testSourcesOutput = ""
        if (!sources['test']['java'].empty) {
            testSourcesOutput = """java {
                                    |           srcDirs = ${sources['test']['java']}
                                    |       }""".stripMargin()
        }
        def resourcesOutput = ""
        if (!sources['main']['resources'].empty) {
            resourcesOutput = """resources {
                                    |           srcDirs = ${sources['main']['resources']}
                                    |       }""".stripMargin()
        }
        def testResourcesOutput = ""
        if (!sources['test']['resources'].empty) {
            testResourcesOutput = """resources {
                                    |           srcDirs = ${sources['test']['resources']}
                                    |       }""".stripMargin()
        }
        def mainSourceSetOutput = ""
        if (!javaSourcesOutput.empty || !resourcesOutput.empty) {
            mainSourceSetOutput = """main {
                                    |       $javaSourcesOutput
                                    |       $resourcesOutput
                                    |   }""".stripMargin()
        }
        def testSourceSetOutput = ""
        if (!testSourcesOutput.empty || !testResourcesOutput.empty) {
            testSourceSetOutput = """test {
                                    |       $testSourcesOutput
                                    |       $testResourcesOutput
                                    |   }""".stripMargin()
        }
        if (!mainSourceSetOutput.empty || !testSourceSetOutput.empty) {
            """sourceSets {
                |   $mainSourceSetOutput
                |   $testSourceSetOutput
                |}""".stripMargin()
        } else {
            ""
        }
    }

    def static generateDependencies(ArrayList<Path> dependencyList, Map allLibraries, Project project) {
        def libs = dependencyList.findAll { it.@type == "library" && it.@level == "project" }
        def projects = dependencyList.findAll { it.@type == "module" }

        def libraryAliases = project.IdeaToGradleConverter.libraryAliases

        def libFilePaths = []
        def libArtifactories = []
        libs.each {
            def libName = it.@name
            if (libraryAliases.containsKey(libName)){
                libArtifactories += libraryAliases.get(libName)
            } else if (allLibraries.containsKey(libName)) {
                def libPath = allLibraries.get(libName)
                libPath['classesPath'].each {
                    libFilePaths += "'$it'"
                }
            }
        }
        def libsOutput = ""
        if (!libFilePaths.empty) {
            libsOutput += "    compile files($libFilePaths)"
        }
        if (!libArtifactories.empty) {
            libsOutput += "    compile( $libArtifactories )"
        }

        def projectsOutput = ""
        projects.each {
            def name = it.@'module-name'
            projectsOutput += "    compile project(':$name')\n"
        }

        if (!libsOutput.empty || !projectsOutput.empty) {
            """dependencies {
                |$libsOutput
                |$projectsOutput
            |}""".stripMargin()
        } else {
            ""
        }
    }

    static def addNewProjectToGradle(String projectName, File gradleSettingsFile) {
        gradleSettingsFile << "include '$projectName'\n"
    }

    @Override
    void apply(Project project) {
        project.extensions.create("IdeaToGradleConverter", IdeaToGradleConverterPluginExtension)

        project.task('convertModule') << {

            println project.IdeaToGradleConverter.libraryAliases
            def ideaProjectLibraries = parseIdeaLibraries(project.fileTree(dir: '.idea/libraries', include: '**/*.xml').findAll(), project)
            File gradleSettingsFile = project.file('settings.gradle')

            Map<String, Project> gradleSubProjects = project.childProjects

            File ideaModuleFile = project.file('.idea/modules.xml')
            if (ideaModuleFile.exists()) {
                parseIdeaProjectModules(ideaModuleFile, project).each { moduleIml ->
                    def moduleDir = moduleIml.parentFile
                    def moduleName = moduleIml.name.replace('.iml', '')
                    if (moduleDir.path != project.projectDir.absolutePath) {
                        if (!gradleSubProjects.containsKey(moduleName)) {
                            if (moduleDir.listFiles().findAll { it.name.endsWith('build.gradle') }.empty) {
                                generateBuildScript(moduleIml, ideaProjectLibraries, project)
                            }
                            addNewProjectToGradle(moduleName, gradleSettingsFile)
                        }
                    }
                }
            }
        }
    }
}

class IdeaToGradleConverterPluginExtension {
    def Map libraryAliases = [:]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
    }
}